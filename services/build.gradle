subprojects {
  apply plugin: "java"
  apply plugin: "io.spring.dependency-management"

  sourceSets {
    main {
      java {
        exclude excludeKeepDirPattern
      }
      resources {
        exclude excludeKeepDirPattern
      }
    }

    test {
      java {
        exclude excludeKeepDirPattern
      }
      resources {
        exclude excludeKeepDirPattern
      }
    }
  }

  test {
    useJUnitPlatform()

    testLogging {
      showStandardStreams = true
      events "SKIPPED", "FAILED"
    }
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    annotationProcessor "org.projectlombok:lombok:${ver.lombok}"
    compileOnly "org.projectlombok:lombok:${ver.lombok}"
    testAnnotationProcessor "org.projectlombok:lombok:${ver.lombok}"
    testCompileOnly "org.projectlombok:lombok:${ver.lombok}"

    implementation project(":libs:todo-util")
    implementation project(":libs:todo-service-common")
    implementation "org.slf4j:slf4j-api"
    implementation "com.google.guava:guava:${ver.guava}"
    implementation "org.apache.commons:commons-lang3"

    testCompileOnly "junit:junit:${ver.junit4}"

    testImplementation project(":libs:todo-test")
    testImplementation "org.slf4j:slf4j-api"
    testImplementation "ch.qos.logback:logback-classic"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${ver.junit5}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${ver.junit5}"
    testImplementation "org.assertj:assertj-core:${ver.assertj}"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${ver.junit5}"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${ver.junit5}"
  }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

project(":services:todo-auth-service-client") {
  dependencyManagement {
    imports {
      mavenBom "org.springframework.boot:spring-boot-dependencies:${ver.springBoot}"
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${ver.springCloud}"
    }
  }

  dependencies {
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
  }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

project(":services:todo-auth-service") {
  apply plugin: "org.springframework.boot"
  apply plugin: "com.bmuschko.docker-spring-boot-application"
  apply plugin: "docker-compose"

  version = "${ver.projectMajor}.1.0"

  sourceSets {
    // integration (functional) tests
    itest {
      java {
        srcDir file("src/itest/java")
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        exclude excludeKeepDirPattern
      }
      resources {
        exclude excludeKeepDirPattern
      }
    }
  }

  configurations {
    itestImplementation.extendsFrom implementation
    itestRuntimeOnly.extendsFrom runtimeOnly
  }

  task itest(type: Test) {
    description = "Run the integration (functional) tests."
    group = "verification"

    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath

    mustRunAfter test
  }

  itest {
    useJUnitPlatform()

    testLogging {
      showStandardStreams = true
      events "SKIPPED", "FAILED"
    }
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${ver.springCloud}"
    }
  }

  dependencies {
    implementation project(":services:todo-auth-service-client")

    implementation "org.hibernate.validator:hibernate-validator"
    implementation "org.hibernate.validator:hibernate-validator-annotation-processor"
    implementation "io.jsonwebtoken:jjwt-api:${ver.jjwt}"
    implementation "io.jsonwebtoken:jjwt-impl:${ver.jjwt}"
    implementation "io.jsonwebtoken:jjwt-jackson:${ver.jjwt}"
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.cloud:spring-cloud-starter-config"
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"
    implementation "io.springfox:springfox-swagger2:${ver.springfoxSwagger}"
    implementation "io.springfox:springfox-swagger-ui:${ver.springfoxSwagger}"
    implementation "org.springframework.cloud:spring-cloud-starter-oauth2"

    testImplementation "org.springframework.boot:spring-boot-starter-test"

    itestAnnotationProcessor "org.projectlombok:lombok:${ver.lombok}"
    itestCompileOnly "org.projectlombok:lombok:${ver.lombok}"

    itestCompileOnly "junit:junit:${ver.junit4}"

    itestImplementation project(":libs:todo-test")
    itestImplementation "org.slf4j:slf4j-api"
    itestImplementation "ch.qos.logback:logback-classic"
    itestImplementation "org.junit.jupiter:junit-jupiter-api:${ver.junit5}"
    itestImplementation "org.junit.jupiter:junit-jupiter-params:${ver.junit5}"
    itestImplementation "org.assertj:assertj-core:${ver.assertj}"
    itestImplementation "org.springframework.boot:spring-boot-starter-test"
    // it's required for rest assured on java 9+
    itestImplementation "org.glassfish.jaxb:jaxb-runtime"
    itestImplementation "io.rest-assured:rest-assured:${ver.restAssured}"

    itestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${ver.junit5}"
    itestRuntimeOnly "org.junit.vintage:junit-vintage-engine:${ver.junit5}"
  }

  springBoot {
    mainClassName = "io.agatsenko.todo.service.auth.AuthServiceApp"
  }

  docker {
    springBootApplication {
      baseImage = dockerImageJava
      maintainer = dockerLabelMaintainer
      tag = "${project.rootProject.name}/${project.name}:${project.version}"
    }
  }

  dockerCompose {
    authServiceITestInfra {
      useComposeFiles = ["${project.project(":").rootDir.toPath().resolve(devDockerComposeFile)}"]
      startedServices = [
          "todo-mongo",
          "todo-config-server",
          "todo-eureka-server",
      ]
      isRequiredBy(itest)
    }
  }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

project(":services:todo-list-service") {
  apply plugin: "org.springframework.boot"
  apply plugin: "com.bmuschko.docker-spring-boot-application"

  version = "${ver.projectMajor}.1.0"

  sourceSets {
    // integration (functional) tests
    itest {
      java {
        srcDir file("src/itest/java")
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        exclude excludeKeepDirPattern
      }
      resources {
        exclude excludeKeepDirPattern
      }
    }
  }

  configurations {
    itestImplementation.extendsFrom implementation
    itestRuntimeOnly.extendsFrom runtimeOnly
  }

  task itest(type: Test) {
    description = "Run the integration (functional) tests."
    group = "verification"

    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath

    mustRunAfter test
  }

  itest {
    useJUnitPlatform()

    testLogging {
      showStandardStreams = true
      events "SKIPPED", "FAILED"
    }
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${ver.springCloud}"
    }
  }

  dependencies {
    implementation project(":services:todo-auth-service-client")

    implementation "org.hibernate.validator:hibernate-validator"
    implementation "org.hibernate.validator:hibernate-validator-annotation-processor"
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.cloud:spring-cloud-starter-config"
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-hystrix"
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
    implementation "io.springfox:springfox-swagger2:${ver.springfoxSwagger}"
    implementation "io.springfox:springfox-swagger-ui:${ver.springfoxSwagger}"
    implementation "org.springframework.cloud:spring-cloud-starter-oauth2"

    testImplementation "org.springframework.boot:spring-boot-starter-test"

    itestAnnotationProcessor "org.projectlombok:lombok:${ver.lombok}"
    itestCompileOnly "org.projectlombok:lombok:${ver.lombok}"

    itestCompileOnly "junit:junit:${ver.junit4}"

    itestImplementation project(":libs:todo-test")
    itestImplementation "org.slf4j:slf4j-api"
    itestImplementation "ch.qos.logback:logback-classic"
    itestImplementation "org.junit.jupiter:junit-jupiter-api:${ver.junit5}"
    itestImplementation "org.junit.jupiter:junit-jupiter-params:${ver.junit5}"
    itestImplementation "org.assertj:assertj-core:${ver.assertj}"
    itestImplementation "org.springframework.boot:spring-boot-starter-test"
    // it's required for rest assured on java 9+
    itestImplementation "org.glassfish.jaxb:jaxb-runtime"
    itestImplementation "io.rest-assured:rest-assured:${ver.restAssured}"

    itestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${ver.junit5}"
    itestRuntimeOnly "org.junit.vintage:junit-vintage-engine:${ver.junit5}"
  }

  springBoot {
    mainClassName = "io.agatsenko.todo.service.List.TodoListServiceApp"
  }

  docker {
    springBootApplication {
      baseImage = dockerImageJava
      maintainer = dockerLabelMaintainer
      tag = "${project.rootProject.name}/${project.name}:${project.version}"
    }
  }
}
